S -> Script

Script -> Statements MWS | MWS

Statements -> Statements MWS Statement | Statement

Statement -> Open | Let | Get

Open -> c o n n e c t WS t o WS AlphaNum
Let -> d e f i n e WS AlphaNum WS a s WS Pattern
Get -> g e t WS ObjExpr WS f r o m WS GraphExpr

GraphExpr -> Intersect | Query | GraphName | SetStartAndFinal | ( MWS GraphExpr MWS )

Intersect -> GraphExpr MWS & MWS GraphExpr
Query -> Pattern
GraphName -> g r a p h WS AlphaNum
SetStartAndFinal -> GraphExpr WS w i t h WS s t a r t s WS a s WS Vertices WS a n d WS f i n a l s WS a s WS Vertices

Vertices -> [ Numbers ] | [ Number @ @ Number ] | [ ]
Numbers -> Number | Number MWS , MWS Numbers

ObjExpr -> Many | c o u n t WS o f WS Many
Many -> e d g e s | Many WS w h e r e WS Cond

Cond -> ( MWS AlphaNum MWS , MWS AlphaNum MWS , MWS AlphaNum MWS ) WS s a t i s f y WS BoolExpr

BoolExpr -> ( MWS BoolExpr MWS ) | AlphaNum WS i s WS " AlphaNum " | AlphaNum WS i s  WS s t a r t | AlphaNum WS i s  WS f i n a l
BoolExpr -> n o t WS BoolExpr | BoolExpr WS a n d WS BoolExpr | BoolExpr WS o r WS BoolExpr

Pattern -> " AlphaNum " | AlphaNum | ( MWS Pattern MWS )
Pattern -> Pattern MWS * | Pattern MWS + | Pattern MWS ? | Pattern MWS ^ MWS Pattern | Pattern MWS Pattern

MWS -> WS | $
WS -> Space WS | Space
Space -> !
NumberND -> DigitNZ Digit | NumberND Digit
Number -> Digit | NumberND
AlphaNum -> _ | Alpha | AlphaNum Alpha | AlphaNum Digit | AlphaNum _
Alpha -> a | b | c | d | e | f | g | h | i | j | k | l | m | n | o | p | q | r | s | t | u | v | w | x | y | z
Digit -> 0 | DigitNZ
DigitNZ -> 1 | 2 | 3 | 4 | 5 | 6 | 7 | 8 | 9


